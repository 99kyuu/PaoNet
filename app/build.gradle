apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
//aspectj
apply plugin: 'android-aspectjx'

android {
    compileSdkVersion 28
    buildToolsVersion rootProject.buildToolsVersion
    defaultConfig {
        applicationId "com.ditclear.paonet"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString(),
                             AROUTER_MODULE_NAME  : project.getName()]

            }
        }
    }

    signingConfigs {
        config {
            keyAlias 'paonet'
            keyPassword '111111'
            storeFile file('debug-keystore.jks')
            storePassword '111111'
        }
    }

    buildTypes {

        debug{
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            debuggable true
            signingConfig signingConfigs.debug
        }
        release {
            applicationIdSuffix ".release"
            versionNameSuffix "-release"
            debuggable true
            signingConfig signingConfigs.debug
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        search {
            applicationIdSuffix ".search"
            versionNameSuffix "-search"
            debuggable true
            signingConfig signingConfigs.debug
        }

        mine{
            applicationIdSuffix ".mine"
            versionNameSuffix "-mine"
            debuggable true
            signingConfig signingConfigs.debug
        }

    }
    dataBinding {
        enabled = true
    }
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }


    sourceSets {
        main{
            //core
            java.srcDirs += 'src/main/core/java'
            //auth
            java.srcDirs += 'src/main/auth/java'
            res.srcDirs  += 'src/main/auth/res'
        }
        release{
            //search
            java.srcDirs += 'src/main/search/java'
            res.srcDirs += 'src/main/search/res'
            manifest.srcFile 'src/main/search/AndroidManifest.xml'
            //mine
            java.srcDirs += 'src/main/mine/java'

        }
        search {
            java.srcDirs += 'src/main/search/java'
            res.srcDirs += 'src/main/search/res'
            manifest.srcFile 'src/main/search/AndroidManifest.xml'
        }

        mine {
            java.srcDirs += 'src/main/mine/java'
            res.srcDirs += 'src/main/mine/res'
        }
    }
}
aspectjx {

    exclude 'android.support'
}

repositories {
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    mavenCentral()
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:design:' + rootProject.supportLibVersion
    implementation 'com.android.support:cardview-v7:' + rootProject.supportLibVersion
    implementation 'com.android.support:recyclerview-v7:' + rootProject.supportLibVersion
    implementation 'com.android.support:palette-v7:' + rootProject.supportLibVersion
    implementation 'com.android.support:support-dynamic-animation:' + rootProject.supportLibVersion
    implementation 'com.android.support:customtabs:' + rootProject.supportLibVersion
    //ViewModel
    implementation "android.arch.lifecycle:extensions:" + rootProject.archLifecycleVersion
    implementation "android.arch.lifecycle:viewmodel:" + rootProject.archLifecycleVersion
    kapt "android.arch.lifecycle:compiler:" + rootProject.archLifecycleVersion
    //kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    //aspectj
    implementation 'org.aspectj:aspectjrt:1.8.13'
    //rx android
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.2'
    //autodispose
    implementation 'com.uber.autodispose:autodispose:0.8.0'
    implementation 'com.uber.autodispose:autodispose-android-archcomponents:0.8.0'
    implementation 'com.google.android:flexbox:0.3.1'
    //di
    implementation 'com.google.dagger:dagger:2.16'
    kapt "com.google.dagger:dagger-compiler:2.16"
    //retrofit
    implementation 'com.squareup.retrofit2:retrofit:' + rootProject.retrofitVersion
    implementation 'com.squareup.retrofit2:converter-gson:' + rootProject.retrofitVersion
    implementation 'com.squareup.retrofit2:adapter-rxjava2:' + rootProject.retrofitVersion
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.0'
    implementation 'com.google.code.gson:gson:2.8.2'
    //room
    implementation 'android.arch.persistence.room:runtime:' + rootProject.archRoomVersion
    implementation 'android.arch.persistence.room:rxjava2:' + rootProject.archRoomVersion
    kapt "android.arch.persistence.room:compiler:" + rootProject.archRoomVersion
    // roomigrant
    implementation 'com.github.MatrixDev.Roomigrant:RoomigrantLib:0.1.3'
    kapt 'com.github.MatrixDev.Roomigrant:RoomigrantCompiler:0.1.3'
    //image
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation 'jp.wasabeef:glide-transformations:2.0.1'
    implementation 'us.feras.mdv:markdownview:1.1.0'
    implementation 'org.jsoup:jsoup:1.11.1'
    implementation 'com.github.GrenderG:Toasty:1.2.5'
    implementation 'com.github.franmontiel:PersistentCookieJar:v1.0.1'
    //Router
    implementation 'com.alibaba:arouter-api:1.4.1'
    kapt 'com.alibaba:arouter-compiler:1.2.2'
    ///////////unit test///////////////
    testImplementation 'org.mockito:mockito-core:2.15.0'
    testImplementation 'junit:junit:4.12'
    testImplementation 'com.github.fabioCollini.daggermock:daggermock:0.8.4'
    //////////ui test//////////////////
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    })
}

//https://github.com/google/dagger/issues/306
gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xmaxerrs" << "500" // or whatever number you want
    }
}